#include <ESP8266WiFi.h>
#include <ESP8266WebServer.h>

// WiFi credentials
const char* ssid = "xxx";
const char* password = "12345678";

// Create web server on port 80
ESP8266WebServer server(80);

const int ledPin = 2; // D4 on NodeMCU 

// HTML Page with CSS
String htmlPage() {
  String page = "<!DOCTYPE html><html>";
  page += "<head><title>ESP8266 LED Control</title>";
  page += "<style>";
  page += "body { font-family: Arial; text-align: center; background: #f2f2f2; }";
  page += "h1 { color: #333; }";
  page += "button { padding: 15px 40px; font-size: 20px; margin: 15px; ";
  page += "border: none; border-radius: 10px; cursor: pointer; }";
  page += ".on { background-color: #4CAF50; color: white; }";
  page += ".off { background-color: #f44336; color: white; }";
  page += "button:hover { opacity: 0.8; }";
  page += "</style></head>";
  page += "<body>";
  page += "<h1>ESP8266 LED Control</h1>";
  page += "<p><a href=\"/on\"><button class='on'>LED ON</button></a></p>";
  page += "<p><a href=\"/off\"><button class='off'>LED OFF</button></a></p>";
  page += "</body></html>";
  return page;
}

void setup() {
  Serial.begin(115200);
  pinMode(ledPin, OUTPUT);
  digitalWrite(ledPin, LOW);

 
  WiFi.begin(ssid, password);
  Serial.print("Connecting to WiFi");
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("\nWiFi connected!");
  Serial.print("IP Address: ");
  Serial.println(WiFi.localIP());

 
  server.on("/", []() {
    server.send(200, "text/html", htmlPage());
  });

  server.on("/on", []() {
    digitalWrite(ledPin, HIGH);
    server.send(200, "text/html", htmlPage());
  });

  server.on("/off", []() {
    digitalWrite(ledPin, LOW);
    server.send(200, "text/html", htmlPage());
  });

  server.begin();
  Serial.println("HTTP server started");
}

void loop() {
  server.handleClient();
}
